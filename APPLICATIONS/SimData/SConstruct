# -*-python-*-

# SimData: Data Infrastructure for Simulations
# Copyright (C) 2002, 2003 Mark Rose <tm2@stm.lbl.gov>
# 
# This file is part of SimData.
# 
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
# 
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.


#############################################################################
#
# This is a preliminary build system based on SCons (http://www.scons.org).
#
#   * It is intended for testing only at this point, and will 
#     not immediately replace the existing make, distutils, and 
#     msvs project systems.  
#
#   * So far this script has been developed only under Linux, so
#     it will likely have major holes under Windows.  
#
#   * Please report any problems or patches to 
#                http://csp.sourceforge.net/forum
#
# --MR
#############################################################################


print "================================================"
print "             SimData Construction"
print "================================================"

from build_support import *
from build_config import *

EnsureSConsVersion(0,9)
EnsurePythonVersion(2,2)

if ARGUMENTS.get('PLATFORM', 0):
	env = Environment(PLATFORM = ARGUMENTS['PLATFORM'])
else:
	env = Environment()

ConfigPython(env)

package = Package(env)
env['PACKAGE'] = package

info = package.info
info.package = 'SimData'
info.description = 'Simulation Data Library'
info.version = '0.4.0'
info.author = 'Mark Rose'
info.contact = '<mkrose@users.sourceforge.net>'
info.homepage = 'http://csp.sourceforge.net/wiki/SimData'
info.license = 'GNU General Public License, version 2 or later'

options = Options('local_config.py', ARGUMENTS)
options.Add('CXX', 
                help='C++ compiler')
options.Add('LINK', 
                help='linker')
options.Add('ARCHIVE_FORMATS', 
                help='File format(s) for distribution archives',
                default=None)
options.Add('PACKAGE_PREFIX', 
                help='Installation path for the SimData package', 
                default=env['PYTHON_LIB'])
options.Add('INCLUDE_PREFIX', 
                help='Installation path for the SimData headers and interfaces', 
                default=env['PYTHON_INC'])
options.Update(env)
Help(options.GenerateHelpText(env))

# Configuration tests

conf = CustomConfigure(env)
if not conf.checkSwig('1.3.16', ('1.3.18','1.3.19')):
	print
	print "   SimData requires SWIG 1.3.16, 1.3.17, 1.3.20, or newer.  If a"
	print "   compatible version of SWIG is already installed, check that is"
	print "   it in the default path.  SWIG can be downloaded from"
	print "   http://www.swig.org"
 	print
 	Exit(1)
env = conf.Finish()

ConfigPlatform(env)

# Export('env', 'construct', 'Package')
Export('env')

builddir = SelectBuildDir(env, "Build")
BuildDir(builddir, 'Source')

SUBDIRS = (builddir, 'Doc')

for subdir in SUBDIRS:
	SConscript(subdir+'/SConscript')

package.addManifest("MANIFEST.IN")
package.sdist()
package.bdist()

