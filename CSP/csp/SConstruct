# -*- python -*-
#
# Copyright 2004 Mark Rose <mkrose@users.sourceforge.net>
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA


print 'COMBAT SIMULATOR PROJECT - Build System'

HELP = """
COMMON TARGETS:

  scons config        check for necessary headers and libraries
  scons cspsim        build the main flight simulator client
  scons indexserver   build the master server needed for multiplayer
  scons all           build everything
  scons dox           generate doxygen documentation
  scons test          run unittests
"""

from tools import build
import sys
import os


build.CheckSConsVersion('0.96.1')
build.CheckPythonVersion('2.2.2')

# several global targets (e.g. 'all') are shared between copied subenvironments;
# this is ok and the warning is annoying
import SCons.Warnings
SCons.Warnings.suppressWarningClass(SCons.Warnings.DuplicateEnvironmentWarning)

def GlobalConfig(env):
	print ':: csp config'
	conf = build.CustomConfigure(env)
	valid = 1
	valid = conf.CheckSwig('1.3.20') and valid
	valid = conf.CheckCommandVersion('ccgnu2', 'ccgnu2-config --version', '1.3.19') and valid
	valid = conf.CheckPkgConfig('sigc++-2.0', version='2.0.6', lib_name='sigc') and valid
	valid = conf.CheckCommandVersion('sdl', 'sdl-config --version', '1.2.5') and valid
	if not valid:
		print 'Missing dependencies; see http://csp.sf.net/wiki/Current_build_dependencies for more information.'
		Exit(1)
	env = conf.Finish()
	env.ParseConfig('ccgnu2-config --flags --libs')
	env.ParseConfig('pkg-config --cflags --libs sigc++-2.0')
	env.ParseConfig('sdl-config --cflags --libs')


PACKAGES = [
	'csplib',
	'modules',
	'cspsim',
]

INCLUDE = [
	'#/..',
	build.PYTHON_INCLUDE,
]


CXX = 'g++-4.0'
CXXFLAGS = '-O2 -g -W -Wall -pedantic -Wno-long-long'
SWIGCXXFLAGS = '-O2 -g'
SWIGFLAGS = '-python -c++'
ARFLAGS = ['cr']


opts = Options()

env = Environment(CXX=CXX,
                  CPPPATH=INCLUDE,
                  CXXFLAGS=CXXFLAGS,
                  CXXFILESUFFIX='.cpp',
                  SWIGFLAGS=SWIGFLAGS,
                  SWIGCXXFLAGS=SWIGCXXFLAGS,
                  SWIGINCLUDES='$_CPPINCFLAGS',
                  ARFLAGS=ARFLAGS,
                  options=opts)

Help(HELP + opts.GenerateHelpText(env))

build.GlobalSetup(env, config=GlobalConfig, default_message='No targets specified; try scons -h for help.')
build.BuildPackages(env, PACKAGES)

