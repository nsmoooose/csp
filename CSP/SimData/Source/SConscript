# -*- python -*-
#
# Copyright 2004 Mark Rose <mkrose@users.sourceforge.net>
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA

from glob import glob

Import('env build')


SOURCES = [
	'BaseType.cpp',
	'DataArchive.cpp',
	'DataManager.cpp',
	'Date.cpp',
	'Enum.cpp',
	'ExceptionBase.cpp',
	'External.cpp',
	'FileUtility.cpp',
	'GeoPos.cpp',
	'HashUtility.cpp',
	'InterfaceRegistry.cpp',
	'Interpolate.cpp',
	'Key.cpp',
	'Link.cpp',
	'List.cpp',
	'LogStream.cpp',
	'LUT.cpp',
	'Math.cpp',
	'Matrix3.cpp',
	'Noise.cpp',
	'Object.cpp',
	'Path.cpp',
	'Quat.cpp',
	'Random.cpp',
	'Real.cpp',
	'ThreadBase.cpp',
	'Timing.cpp',
	'TypeAdapter.cpp',
	'Vector3.cpp',
	'Version.cpp',
]


cSimData_wrap, cSimData_module = env.Swig('cSimData.i', SWIGINCLUDES='-ISimData/Include')
copy_module = env.CopyFile('#/SimData/SimData/cSimData.py', cSimData_module)
env.Alias(['shared', 'all'], copy_module)
INTERFACES = [env.SwigWrapper(cSimData_wrap)]

shlib = env.SharedLibrary('#/SimData/SimData/cSimData', INTERFACES + SOURCES)
build.MarkVersionSource(env, shlib)
env.Alias(['shared', 'all'], shlib)

if not build.IsWindows(env):
	stlib = env.StaticLibrary('#/SimData/SimData/SimData', SOURCES)
	build.MarkVersionSource(env, stlib)
	env.Alias('static', stlib)
	env.Clean('all', stlib)

